class DamNumGMHasher abstract {
	abstract virtualscope uint hash(Object o);
	abstract virtualscope bool eq(Object o0, Object o1);
	static void hashCombine(out uint seed, uint other) {
		seed ^= other + 0x9E3779B9 + (seed << 6) + (seed >> 2);
	}
}

class DamNumGMBoxedInt {
	int val;
	static DamNumGMBoxedInt create(int val) {
		let ret = new();
		ret.val = val;
		return ret;
	}
	DamNumGMBoxedInt clone() {
		let ret = new();
		ret.val = self.val;
		return ret;
	}
}
class DamNumGMIntHasher : DamNumGMHasher {
	override uint hash(Object o) { return DamNumGMBoxedInt(o).val; }
	override bool eq(Object o0, Object o1) { return DamNumGMBoxedInt(o0).val == DamNumGMBoxedInt(o1).val; }
}

class DamNumGMBoxedUInt {
	uint val;
	static DamNumGMBoxedUInt create(uint val) {
		let ret = new();
		ret.val = val;
		return ret;
	}
	DamNumGMBoxedUInt clone() {
		let ret = new();
		ret.val = self.val;
		return ret;
	}
}
class DamNumGMUIntHasher : DamNumGMHasher {
	override uint hash(Object o) { return DamNumGMBoxedUInt(o).val; }
	override bool eq(Object o0, Object o1) { return DamNumGMBoxedUInt(o0).val == DamNumGMBoxedUInt(o1).val; }
}

class DamNumGMBoxedDouble {
	double val;
	static DamNumGMBoxedDouble create(double val) {
		let ret = new();
		ret.val = val;
		return ret;
	}
	DamNumGMBoxedDouble clone() {
		let ret = new();
		ret.val = self.val;
		return ret;
	}
}

class DamNumGMBoxedString {
	String val;
	static DamNumGMBoxedString create(String val) {
		let ret = new();
		ret.val = val;
		return ret;
	}
	DamNumGMBoxedString clone() {
		let ret = new();
		ret.val = self.val;
		return ret;
	}
}

class DamNumGMHashSentinel {}

class DamNumGMHashUnoccupied {}

class DamNumGMKVStorage {
	Array<Object> keys;
	Array<Object> values;
	uint loadCount;

	DamNumGMHashUnoccupied unoccupied;

	DamNumGMHashSentinel empty;
	DamNumGMHashSentinel tombstone;

	const SEED32 = 0x27220a95u;
	private static uint hash(Object key, DamNumGMHasher hasher) {
		let h = hasher.hash(key);
		return h * SEED32;
	}

	Object get(Object key, DamNumGMHasher hasher) {
		let hash = hash(key, hasher);
		let size = uint(self.keys.size());
		let index = hash % size;
		let i = index;
		while (true) {
			let kb = self.keys[i];
			if (kb == self.empty) {
				break;
			}
			if (kb != self.tombstone && hasher.eq(kb, key)) {
				return self.values[i];
			}
			i = (i + 1) % size;
		}
		return self.unoccupied;
	}

	DamNumGMKVStorage resized(uint newSize, DamNumGMHasher hasher) {
		let newKV = new("DamNumGMKVStorage");
		newKV.keys.resize(newSize);
		newKV.values.resize(newSize);

		newKV.unoccupied = self.unoccupied;
		newKV.empty = self.empty;
		newKV.tombstone = self.tombstone;

		newKV.clear();

		for (let i = 0; i < self.keys.size(); i++) {
			let k = self.keys[i];
			if (k != self.empty && k != self.tombstone) {
				newKV.insert(k, self.values[i], hasher);
			}
		}

		return newKV;
	}

	void insert(Object key, Object value, DamNumGMHasher hasher) {
		let hash = hash(key, hasher);
		let size = uint(self.keys.size());
		let index = hash % size;
		{
			let i = index;
			while (true) {
				let kb = self.keys[i];
				if (kb == self.empty) {
					break;
				}
				if (kb != self.tombstone && hasher.eq(kb, key)) {
					self.values[i] = value;
					return;
				}
				i = (i + 1) % size;
			}
		}
		{
			let i = index;
			while (true) {
				let kb = self.keys[i];
				if (kb == self.empty || kb == self.tombstone) {
					self.keys[i] = key;
					self.values[i] = value;
					if (kb == self.empty) {
						self.loadCount += 1;
					}
					break;
				}
				i = (i + 1) % size;
			}
		}
	}

	Object remove(Object key, DamNumGMHasher hasher) {
		let hash = hash(key, hasher);
		let size = uint(self.keys.size());
		{
			let index = hash % size;
			let i = index;
			while (true) {
				let kb = self.keys[i];
				if (kb == self.empty) {
					break;
				}
				if (kb != self.tombstone && hasher.eq(kb, key)) {
					let val = self.values[i];
					self.keys[i] = self.tombstone;
					self.values[i] = NULL;
					return val;
				}
				i = (i + 1) % size;
			}
		}
		return self.unoccupied;
	}

	bool isEmpty() {
		for (let i = 0; i < self.keys.size(); i++) {
			let k = self.keys[i];
			if (k != self.empty && k != self.tombstone) {
				return false;
			}
		}
		return true;
	}

	void clear() {
		for (let i = 0; i < self.keys.size(); i++) {
			self.keys[i] = self.empty;
			self.values[i] = NULL;
		}
		self.loadCount = 0u;
	}
}

class DamNumGMHashMapIterator {
	private DamNumGMKVStorage m;
	private int i;

	static DamNumGMHashMapIterator internalCreate(DamNumGMKVStorage kv) {
		let ret = new();
		ret.m = kv;
		ret.i = 0;
		return ret;
	}

	Object, Object next() {
		while (self.i < m.keys.size()) {
			let k = m.keys[self.i];
			let v = m.values[self.i];
			self.i += 1;
			if (k != self.m.empty && k != self.m.tombstone) {
				return k, v;
			}
		}
		return self.m.unoccupied, NULL;
	}
}

class DamNumGMHashMap {
	private DamNumGMKVStorage kv;
	private DamNumGMHasher hasher;

	static DamNumGMHashMap create(DamNumGMHasher hasher) {
		let ret = new();
		ret.kv = new("DamNumGMKVStorage");
		ret.kv.keys.resize(8);
		ret.kv.values.resize(8);
		ret.kv.unoccupied = new("DamNumGMHashUnoccupied");
		ret.kv.empty = new("DamNumGMHashSentinel");
		ret.kv.tombstone = new("DamNumGMHashSentinel");
		ret.kv.clear();
		ret.hasher = hasher;
		return ret;
	}

	Object get(Object key) {
		return self.kv.get(key, self.hasher);
	}

	bool containsKey(Object key) {
		return !(self.kv.get(key, self.hasher) is "DamNumGMHashUnoccupied");
	}

	void resize(uint newSize) {
		self.kv = self.kv.resized(newSize, hasher);
	}

	void insert(Object key, Object value) {
		if (self.kv.loadCount + 1 > uint(self.kv.keys.size() * 0.5)) {
			self.resize(self.kv.keys.size() * 2);
		}
		self.kv.insert(key, value, self.hasher);
	}

	Object remove(Object key) {
		return self.kv.remove(key, self.hasher);
	}

	bool isEmpty() {
		return self.kv.isEmpty();
	}
	
	void clear() {
		self.kv.clear();
	}

	DamNumGMHashMapIterator iter() {
		return DamNumGMHashMapIterator.internalCreate(self.kv);
	}

	String debugRepr() {
		let ret = "";
		for (let i = 0; i < self.kv.keys.size(); i++) {
			ret.appendFormat("%d : ", i);
			let k = self.kv.keys[i];
			if (k == self.kv.empty) {
				ret.appendFormat("EMPTY");
			} else if (k == self.kv.tombstone) {
				ret.appendFormat("TOMBSTONE");
			} else {
				ret.appendFormat("%p -> %p", k, self.kv.values[i]);
			}
			if (i != self.kv.keys.size() - 1) {
				ret.appendFormat("\n");
			}
		}
		return ret;
	}

	double loadFactor() {
		return double(self.kv.loadCount) / double(self.kv.values.size());
	}
	uint bucketCount() {
		return self.kv.values.size();
	}
}

class DamNumGMHashSetIterator {
	private DamNumGMHashMapIterator i;

	static DamNumGMHashSetIterator internalCreate(DamNumGMHashMapIterator i) {
		let ret = new();
		ret.i = i;
		return ret;
	}

	Object next() {
		return self.i.next();
	}
}

class DamNumGMHashSet {
	private DamNumGMHashMap hm;

	static DamNumGMHashSet create(DamNumGMHasher hasher) {
		let ret = new();
		ret.hm = DamNumGMHashMap.create(hasher);
		return ret;
	}

	bool contains(Object key) {
		return self.hm.containsKey(key);
	}

	void resize(uint newSize) {
		self.hm.resize(newSize);
	}

	void insert(Object key) {
		self.hm.insert(key, NULL);
	}

	Object remove(Object key) {
		return self.hm.remove(key);
	}

	bool isEmpty() {
		return self.hm.isEmpty();
	}

	void clear() {
		self.hm.clear();
	}

	DamNumGMHashSetIterator iter() {
		return DamNumGMHashSetIterator.internalCreate(self.hm.iter());
	}

	String debugRepr() {
		return self.hm.debugRepr();
	}
}
